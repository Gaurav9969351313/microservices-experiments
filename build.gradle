buildscript {

	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("se.transmode.gradle:gradle-docker:1.2")
	}

}

subprojects {

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: "docker"

	group = 'com.typesafe.microservices-experiments'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	ext {
		springCloudVersion = 'Edgware.RELEASE'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	task buildDocker(type: Docker, dependsOn: build) {

		def projectName = "microservices-experiments"
		def jarName = jar.baseName.toLowerCase()
		maintainer = "zouabi.mourad@gmail.com"

		tag = "$projectName/$jarName"
		dockerfile = file('./Dockerfile')
		doFirst {
			copy {
				from jar
				into stageDir
			}
		}
	}

}